// Indicator
path /indicators {
  read() { true }
}

path /indicators/{indicatorId} is Indicator {
  read() { true }
}

type Indicator {
  name: String,
  unit: String,
  reference: Reference
}

// Log
path /logs/{userId} is Log[] {
  read() { isCurrentUser(userId) }
  write() { isCurrentUser(userId) }
}

type Log {
  date: InitialTimestamp,
  text: String,
}

// Reference
path /references/{userId} is Reference[] {
  read() { isCurrentUser(userId) }
}

type Reference {
  min: Number,
  max: Number
}

// Report
path /reports/{userId} is Report[] {
  read() { isCurrentUser(userId) }
}

type Report {
  date: InitialTimestamp,
  text: String,
  indicators: ReportIndicator[]
}

type ReportIndicator {
  value: Number
}

// User
path /users/{userId} is User {
  read() { true }
  write() { isCurrentUser(userId) }
}

type User {
  name: String,
  imageUrl: String | Null
  gender: Gender

  // TODO: Move to a private object
  birthdate: PastTimestamp,
}

// Misc
// Types
type InitialTimestamp extends Number {
  validate() { this == (prior(this) == null ? now : prior(this)) }
}

type PastTimestamp extends Number {
  validate() { this < now }
}

type Gender extends String {
  validate() { this == 'male' || this == 'female' || this == 'other' }
}

// Functions
function isAuthenticated() {
  auth != null
}

function isCurrentUser(uid) {
  isAuthenticated() && auth.uid == uid
}
